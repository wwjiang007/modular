# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/flatbuffers/flatbuffer.gni")

group("all") {
  deps = [
    ":device_runner",
    ":dummy_device_shell",
    "userpicker_device_shell",
  ]
}

source_set("password_hash") {
  sources = [
    "password_hash.cc",
    "password_hash.h",
  ]

  deps = [
    "//lib/ftl",
    "//third_party/boringssl",
  ]
}

flatbuffer("users") {
  sources = [
    "users.fbs",
  ]
}

executable("device_runner") {
  sources = [
    "device_runner.cc",
    "user_controller_impl.cc",
    "user_controller_impl.h",
  ]

  deps = [
    ":password_hash",
    ":users",
    "//application/lib/app",
    "//application/services",
    "//apps/ledger/services",
    "//apps/ledger/services/internal",
    "//apps/modular/lib/fidl:array_to_string",
    "//apps/modular/lib/fidl:scope",
    "//apps/modular/services/config",
    "//apps/modular/services/device",
    "//apps/modular/services/user:private",
    "//apps/mozart/services/presentation",
    "//apps/mozart/services/views",
    "//lib/ftl",
    "//lib/mtl",
  ]
}

executable("dummy_device_shell") {
  sources = [
    "dummy_device_shell.cc",
  ]

  deps = [
    "//apps/modular/lib/fidl:single_service_view_app",
    "//apps/modular/services/device",
    "//apps/mozart/services/views",
    "//lib/ftl",
    "//lib/mtl",
  ]
}

source_set("unittests") {
  testonly = true

  sources = [
    "password_hash_unittests.cc",
  ]

  deps = [
    ":password_hash",
    "//third_party/boringssl",
    "//third_party/gtest",
  ]
}
